plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}


dependencies {
    implementation project(":kondor-core")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.squareup:kotlinpoet:1.8.0"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"

    testImplementation "io.strikt:strikt-core:$striktVersion"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitLauncherVersion"

}


tasks {
    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed", "passed"
        }
    }
}

archivesBaseName = 'kondor-tools'

//        name 'kondor-tools'
//        description 'A suite of tools to work with Kondor json library'


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = archivesBaseName
            version = project.version

            pom {
                name = 'kondor-tools'
                description = 'A suite of tools to work with Kondor json library'
                url = 'https://github.com/uberto/kondor-json'
                inceptionYear = '2021'
                scm {
                    url = 'https://github.com/uberto/kondor-json'
                    connection = 'https://github.com/uberto/kondor-json.git'
                    developerConnection = 'git@github.com:uberto/kondor-json.git'
                }
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'UbertoBarbini'
                        name = 'Uberto Barbini'
                        email = 'uberto.gama@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = findProperty('nexusUsername')
                password = findProperty('nexusPassword')
            }
        }
    }
}