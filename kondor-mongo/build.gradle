plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

kotlin {
    jvmToolchain(17)
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
    maxHeapSize = "2g"
    testLogging {
//        showStandardStreams = true
        events "skipped", "failed", "passed"
    }
}

dependencies {
    api project(":kondor-core")

    api "org.mongodb:mongodb-driver-sync:4.8.2"

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"

    testImplementation "io.strikt:strikt-core:$striktVersion"

    testImplementation("org.testcontainers:testcontainers:$testContainerVersion")
    testImplementation ("org.testcontainers:junit-jupiter:$testContainerVersion")
    testImplementation("org.testcontainers:mongodb:$testContainerVersion")

    testImplementation project (":kondor-tools")
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:1.8.0"
}



archivesBaseName = 'kondor-mongo'

//        name 'kondor-mongo'
//        description 'A library to use Mongo with Kondor converters'
//        url 'https://github.com/uberto/kondor-json'
//        inceptionYear '2023'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = archivesBaseName
            version = project.version

            pom {
                name = 'kondor-mongo'
                description = 'A library to use Mongo with Kondor converters'
                url = 'https://github.com/uberto/kondor-json'
                inceptionYear = '2023'
                scm {
                    url = 'https://github.com/uberto/kondor-json'
                    connection = 'https://github.com/uberto/kondor-json.git'
                    developerConnection = 'git@github.com:uberto/kondor-json.git'
                }
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'UbertoBarbini'
                        name = 'Uberto Barbini'
                        email = 'uberto.gama@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = findProperty('nexusUsername')
                password = findProperty('nexusPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}